attribute(doc("
The anna command line prompt, also known as a repl."));


def repl(reflection.Continuation debugFrame = ?)
    (doc(
	"This is the so called repl of anna. Repl is short for Read, Evaluate
and Print-Loop, and is what implements the interactive prompt of a
programming language. Just as one would expect from the name, the repl
function will repeatedly read lines of input from the user, evaulate
them as program code and print the output value to the screen.",
	"The Anna repl is used internally in two situations. Firstly, if Anna
is called without specifying an application to run, the repl is
invoked. Secondly, the interactive Anna debugger is simply a repl
prompt with some additional debugging functions enabled. The debugging
functions are enabled by sending in a non-null value for the
debugFrame parameter.",
	"The prompt is implemented using the readLine library, and supports any
keybindings supported by readLine. The history is also saved on exit,
so commands from previous Anna sessions can be reused using the
standard readline history functionality. The string that is returned
from the call to readline is converted into an AST using <a
path='parser' member='parse'>parser.parse</a> function, inserted into
a larger AST representing an entire module, and is finally compiled
using the <a path='parser' member='compile'>parser.compile</a>
function. The resulting function is then executed and the output is
then printed to the screen.",
	"The repl has some special features, including",
	"
<ul>
  <li>the <code>help</code> and <code>dir</code>
commands, used for showing help on a type or method, and for printing
the members of a type, respectively,
  <li>The <code>ans</code> variable, which always points to
the value of the last command to execute and</li>
  <li>the <code>exit</code> and <code>quit</code> commands, both of which break the repl loop.</li>
</ul>",
	), internal)
{
    use(parser);

    doLoop := 1;
    
    print("Welcome to anna!

Type «help» for more information.");

    /*
      If the user writes e.g. a use clause or an extend clause, we
      don't actually execute it, we instead save that clause away in
      this list. On every subsequent call, we prepend these
      expressions to the one the user wrote.
     */
    globalClause :== «Node»[ast(use(replTool)), ast(expand(replMacros))];
    localClause :== «Node»[];
    
    saveHistory :== {
	if(readLine.history.write(historyFile))
	{
	    print("Failed to save history");
	    print(unix.error.errorString(unix.error.errno));
	}
    }

    /*
      Function for controlled exit
     */
    die :== {
	print("Bye!");
	saveHistory();
	doLoop = ?;
    }

    /*
      We use a continuation of the previous function call in order to
      sneak a peek of all the variables already declared and then add
      them to the new function used to evaluate the next statement.
    */
    previousContinuation := ? as reflection.Continuation;    

    /*
      Set up the history list
    */
    historyFile :== unix.env["HOME"] ~ "/.anna_history";
    readLine.history.read(historyFile);

    /*
      Set up the various short cut actions available, such as showing
      a help text when the user writes «help».
    */

    helpText :== "Welcome to Anna!

If this is your first time using Anna, you might want to
check out the tutorial available at
http://liljencrantz.github.com/anna/tutorial.html

If you want to ask for help on a particular object directly from the
interpreter, you can type «help(OBJECT)». For example, to show help on
the lang module, type «help(lang)». 

If you want to list the contents of an object, type «dir(OBJECT)». For
example, to show the contents of the Int type, type «dir(Int)».

Type «warranty», «copyright» or «license» for legal information.";

    copyrightText :== "Copyright(C) 2005-2012 Axel Liljencrantz
All rights reserved.";
    helloText:= "Hi!";
    warrantyText :== "This software is provided by the copyright holders and contributors \"as is\" and
any express or implied warranties, including, but not limited to, the implied
warranties of merchantability and fitness for a particular purpose are
disclaimed. In no event shall the copyright owner or contributors be liable for
any direct, indirect, incidental, special, exemplary, or consequential damages
(including, but not limited to, procurement of substitute goods or services;
loss of use, data, or profits; or business interruption) however caused and
on any theory of liability, whether in contract, strict liability, or tort
(including negligence or otherwise) arising in any way out of the use of this
software, even if advised of the possibility of such damage.";

    licenseText :== copyrightText ~ "

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met: 

1. Redistributions of source code must retain the above copyright notice, this
   list of conditions and the following disclaimer. 
2. Redistributions in binary form must reproduce the above copyright notice,
   this list of conditions and the following disclaimer in the documentation
   and/or other materials provided with the distribution.

" ~ warrantyText;
 
    action :== «Node, reflection.Block»[
	ast(hello): {print(helloText)},
	ast(help): {print(helpText)},
	ast(copyright): {print(copyrightText)},
	ast(license): {print(licenseText)},
	ast(warranty): {print(warrantyText)},
	ast(quit): die,
	ast(exit): die,
	];

    actionMapping :== «String, def Node (parser.Call input)»[];

    firstPrompt := "anna> ";

    if(debugFrame)
    {
	/*
	  Add some additional functionality for debugger prompts
	 */

	// continue exits the debugger
	action[ast(continue)] = die;

	// break someFunction:someLineNumber inserts a breakpoint
	actionMapping["break"] = def (parser.Call input){
	    use(parser);
	    if( input.count != 1 or !(input[0].callTo?("__mapping__")))
	    {
		print("Invalid breakpoint");
		return ?;
	    }
	    mapping := input[0] as Call;
	    ast(debug.breakpoint(%fun, %line)) % [
		"fun": mapping[0],
		"line": mapping[1],
		];
	}

	// Add a frame variable to the global scope, pointing to the current continuation frame
	globalClause.push(ast(frame :== %frame) % ["frame": parser.Dummy(?, debugFrame)]);

	// Create a struct of all the variables in the current continuation frame, 
	// and add that into a use clause in the function
	varStruct :== ast(struct());
	debugFrame.member.each(val)
	{
	  el := ast(%name: %value) % [
		"name": Identifier(?, val.name),
		"value": (if(val.value){Dummy(?, val.value)} else {NullLiteral(?)}) as Node];
	    varStruct.push(el);
	}
	localClause.push(ast(use(%varStruct)) % ["varStruct": varStruct]);

	globalClause.push(ast(use(debug)));

	firstPrompt = "debug> ";
    }
    
    secondPrompt := " " * firstPrompt.count;

    prev := ? as Any;
    /*
      Start the main repl loop
    */
    while(doLoop)
    {
        line := "".thaw;
        parsedAst := ? as Call;
	
        prompt := firstPrompt;
	
        parseError :== collect(error.CompilationError)
	{
	    while(1)
	    {
	        lineElement :== readLine.readLine(prompt);
		prompt = secondPrompt;
		if(!lineElement)
		{
		    print();
		    die();
		}
		
		line ~= lineElement;
		
	        incompleteError :== collect(error.IncompleteError){
		    parsedAst = parse(line) as Call;
		}
		if(parsedAst or incompleteError.empty?)
		{
		    break
		}
		line ~= "\n";
	    }
	}
	parseError.each(error)
	{
	    print(error.message);
	}

	if(line.count > 0)
	{
	    readLine.history.add(line);
	    saveHistory();
	}
	      
	/*
	  If the user entered multiple expressions on a single line,
	  execute them one at a time. Stop the loop on error.
	*/
	parsedAst.each(astItem)
	{
	    /*
	      Check if this is one of the short cut actions like
	      «help», and if so, execute the corresponding action.
	    */
	    if(astItem in action)
	    {
		action[astItem]();
		continue;
	    }

	    actionMapping.each(name, call){
		if(astItem.callTo?(name))
		{
		    astItem = call(astItem as parser.Call);
		}
	    }
	    
	    run := 1;
  	    isGlobal :== (astItem.callTo?("use") or astItem.callTo?("expand"));
	    
	    /*
	      Add all variables that have been previously declared by
	      the user. We do this by introspecting a continuation of
	      the previous function invocation and extracting all it's
	      variable names and values.
	    */
	    variableDeclaration := ast(nothing());
	    previousContinuation.member.each(memb)
	    {
		if(memb.name != "ans")
		{
		    variableDeclaration.push(
			ast(%decl(
				%name,
				?,
				%value as %Type,
				{})) % [
				    "decl": (if(memb.mutable?) {ast(__var__)} else {ast(__const__)}),
				    
			    "name": Identifier(?, memb.name), 
			    "value": (if(memb.value){Dummy(?, memb.value)}else{ast(? as Any)}) as Node,
			    "Type": Dummy(?, memb.type),
			    ]
			);
		}
	    }
	    variableDeclaration ~= localClause;
	    /*
	      Make an ast representing an entire module with our fancy
	      function in it.
	    */
  	    moduleAst := ast(
		nothing(
		    var reflection.Continuation %cont;
		    def %fun()
		    {
			%cont = reflection.currentContinuation; 
			%variable; 
			ans := %ans;
			%ast;
		    })) % [
			"fun": Identifier(?, "!fun"), 
			"ast": astItem, 
			"cont": Identifier(?, "!cont"), 
			"variable": variableDeclaration,
			"ans": (if(prev){Dummy(?, prev)}else{ast(? as Any)}) as Node,
			];
	    if(isGlobal)
	    {
		run = ?;
	        moduleAst = ast(nothing(%ast)) % ["ast": astItem];
	    }
	    
	    /*
	      Add all use and extend clauses from previous function invocations
	    */
	    globalClause.each(node){
		(moduleAst as Call).push(node);
	    }

	    code :== compile(moduleAst);
	    if(code)
	    {
		if(isGlobal)
		{
		    globalClause.push(astItem);
		}
		
		if(run)
		{
		    /*
		      Use reflection to find our function and run it,
		      then use reflecation to find the continuation of
		      the function invocation so we can introspect it
		      in the next lap.
		    */
//		    print(moduleAst);
		    funMemb :== type(code).member.find(memb){memb.name=="!fun"};
  		    fun :== funMemb.value(code);
		    prev = (fun as reflection.Block)();
		    prev and print(prev);
		    previousContinuation = type(code).member.find(memb){memb.name=="!cont"}.value(code) as reflection.Continuation;
		}
	    }
	    else
	    {
		/*
		  If we encounter an error, stop executing the rest of
		  the expressions on this line.
		*/
		break;
	    }
	}
    }
}

def debugRepl() 
(
    doc("
Don't use this function or rely on its existence. It is used internally by the breakpoint
bytecode in the VM."),
    internal)
{
    debugFrame := reflection.currentContinuation.dynamicFrame.dynamicFrame;    
    repl(debugFrame);
}

def main() 
(
    doc("
Calls the repl.repl function and then exits. This is the
main()-function that is called when no other main function is given on
the command line."), internal)
{
    repl();
}

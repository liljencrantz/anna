/*
error IoError (Error) {}
*/

type File
(
    documentation(
	"A high level object respresenting a filesystem entry such as a file, a directory or a socket. Can be used for checking the status of a file, creating a directory, reading, writing and various other operations. A File object represents a file system path; it does not always point to an actually existing file.")
)
{
    
    var List «String» path (internal, documentation("The path to this file"));
    var Int fd (internal, documentation("If this file is open, this is its file descriptor."));

    def __init__(String filename)
    (
	documentation("Create a new file object.")
    )
    {
	if(cio.isRelative(filename))
	{
	    path = cio.cwd.split(cio.separator, ?);
	    path ~= filename.split(cio.separator, ?);
	} else {
	    path = filename.split(cio.separator, ?);
	}
    }

    def File __join__(String name)
    (
	documentation(
	    "Joins the specified subdirectory to the directory represented by the File object and returns the result.")
    )
    {
	File(filename ~ cio.separator ~ name);
    }
    
    def filenameGetter() (internal)
    {
	"" ~ cio.separator ~ String::convert(cio.separator).join(path)
    }
    
    var String filename (property(filenameGetter), documentation("The name of the file."));

    def String readFile()
    (
	documentation(
	    "Read the contents of the file and return it as a String.")
    )
    {
        (fd := cio.open(this.filename, cio.openMode.readOnly, 0)) or (return ?);
        buff := Buffer();
        res := cio.read(fd, buff, ?);
	cio.close(fd);
	return if(res){buff.encode()} else {?}
    }
    
    def writeFile(String data)
    (
	documentation(
	    "Write the contents of the String into the specified file.")
    )
    {
        (fd := cio.open(
	    this.filename, 
	    cio.openMode.writeOnly ^bitor 
	    cio.openMode.truncate ^bitor 
	    cio.openMode.create, 
	    0o666)) or (return ?); 
        buff := Buffer();
        ok := buff.decode(data) and cio.write(fd, buff, ?);
	return cio.close(fd) and ok;
    }
    
    def open()
    (
	documentation("Open the specified file for reading and writing.")
    )
    {
        fd = cio.open(
	    this.filename, 
	    cio.openMode.readWrite ^bitor cio.openMode.create, 
	    0o666);
	if(fd) {this} else {?};
    }

    def close()
    (
	documentation("Close the specified file for reading and writing.")
    )
    {
        cio.close(fd)
    }

    def write(Object value...)
    (
	documentation(
	    "Write the string representation of the specified objects to the file.")
    )
    {
        b := Buffer();
	value.each(v)
	{
	    b.decode(String::convert(v));
	    cio.write(this.fd, b, ?);
	}
    }
  
    def existsGetter() (internal)
    {
	return if(cio.stat(this.filename)){1} else {?};
    }
    
    var Int exists 
    (
	property(existsGetter), 
	documentation(
	    "This value is non-null if the file pointed to by this object currently exists.")
    );

    def isFileGetter() (internal)
    {
        (val := cio.stat(this.filename)) or (return ?);
	return (val[2] ^bitand cio.statMode.regular) != 0;      
    }

    var Int isFile 
    (
	property(isFileGetter), 
	documentation(
	    "This value is non-null if the file pointed to by this object is a regular file")
    );

    def isDirectoryGetter() (internal)
    {
        (val := cio.stat(this.filename)) or (return ?);
	return (val[2] ^bitand cio.statMode.directory) != 0;      
    }

    var Int isDirectory
    (
	property(isDirectoryGetter), 
	documentation(
	    "This value is non-null if the file pointed to by this object is a directory.")
    );

    def makeDirectory()
    (
	documentation("Creates a directory in the path specified by the File object.")
    )
    {
	(0..this.path.count).each(last)
	{
	    name := "" ~ cio.separator ~ String::convert(cio.separator).join(this.path[0..last+1]);
	    
	    if(!cio.mkdir(name, 0o777) and !(cerror.errno in [cerror.exist]))
	    {
		print(cerror.errorString(cerror.errno), "\n");
		return ?;
	    }
	}
	return 1;
    }
    
    def Object changeDirectory()
    (
	documentation(
	    "Change the current working directory to the directory represented by this File object.")
    )
    {
	cio.cwd = this.filename;
    }
    
    def File currentDirectoryGetter() (static, internal)
    {
	File(cio.cwd)
    }

    var File currentDirectory 
    (
	static, 
	property(currentDirectoryGetter),
        documentation("Returns a File object representing the current working directory")
    );

    def delete()
    (
	documentation("Deletes the file system path represented by the File object.")
    )
    {
	cio.unlink(this.filename);
    }

    def String toString()
    {
	return filename;
    }

    def copyTo(File destination)
    {

	
    }

}

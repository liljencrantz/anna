def main() {
    var n = 500000;
    var bodies = NBodySystem();
    print(bodies.energy(), '\n');
    bodies.advance(n, 0.01);
    print(bodies.energy(), '\n');
}

type Body {

    var Float x;
    var Float y;
    var Float z;
    var Float vx;
    var Float vy;
    var Float vz;
    var Float mass;

    def __init__(){
       this.x=0.0;
       this.y=0.0;
       this.z=0.0;
       this.vx=0.0;
       this.vy=0.0;
       this.vz=0.0;
       this.mass=0.0;
   }

   def offsetMomentum(Float px, Float py, Float pz){
      this.vx = -px / solarMass;
      this.vy = -py / solarMass;
      this.vz = -pz / solarMass;
      this;
   }
}

type NBodySystem {
    var List«Body» bodies;
    
    def __init__(){
	this.bodies = [
            sun(),
            jupiter(),
            saturn(),
            uranus(),
            neptune()
	    ];
	
	var px = 0.0;
	var py = 0.0;
	var pz = 0.0;
	this.bodies.each(body)
	{
	    px += body.vx * body.mass;
	    py += body.vy * body.mass;
	    pz += body.vz * body.mass;
	}
	this.bodies[0].offsetMomentum(px,py,pz);
    }
    
    def advance(Int n, Float dt) {
	
	(0..n).each(nn){ 
	    this.bodies.each(i, iBody){
		(i+1..this.bodies.count).each(j)
		{
		    var jBody = this.bodies[j];
		    var dx = iBody.x - jBody.x;
		    var dy = iBody.y - jBody.y;
		    var dz = iBody.z - jBody.z;
		    
		    var mag = dt * ((dx * dx + dy * dy + dz * dz) ^exp -1.5);
		    var jm = jBody.mass * mag;
		    var im = iBody.mass * mag;
		    iBody.vx -= dx * jm;
		    iBody.vy -= dy * jm;
		    iBody.vz -= dz * jm;
		    
		    jBody.vx += dx * im;
		    jBody.vy += dy * im;
		    jBody.vz += dz * im;
		}
	    }
	    
	    this.bodies.each(body){
		body.x += dt * body.vx;
		body.y += dt * body.vy;
		body.z += dt * body.vz;
	    }
	}
    }
    
    def energy(){
	var e = 0.0;
	
	this.bodies.each(i, iBody){
	    e += 0.5 * iBody.mass *
		( iBody.vx * iBody.vx
		  + iBody.vy * iBody.vy
		  + iBody.vz * iBody.vz );
	    
	    (i+1..this.bodies.count).each(j){
		var jBody = this.bodies[j];
		var dx = iBody.x - jBody.x;
		var dy = iBody.y - jBody.y;
		var dz = iBody.z - jBody.z;
		var distance = ^sqrt(dx*dx + dy*dy + dz*dz);
		e -= (iBody.mass * jBody.mass) / distance;
	    }
	}
	return e;
    }
}

var pi = 3.141592653589793;
var solarMass = 39.478417604357428;
var daysPerYear = 365.24;

def jupiter(){
    var p = Body();
    p.x = 4.84143144246472090;
    p.y = -1.16032004402742839;
    p.z = -0.103622044471123109;
    p.vx = 0.00166007664274403694 * daysPerYear;
    p.vy = 0.00769901118419740425 * daysPerYear;
    p.vz = -0.0000690460016972063023 * daysPerYear;
    p.mass = 0.000954791938424326609 * solarMass;
    p;
}

def saturn(){
    var p = Body();
    p.x = 8.34336671824457987;
    p.y = 4.12479856412430479;
    p.z = -0.403523417114321381;
    p.vx = -0.00276742510726862411 * daysPerYear;
    p.vy = 0.00499852801234917238 * daysPerYear;
    p.vz = 0.0000230417297573763929 * daysPerYear;
    p.mass = 0.000285885980666130812 * solarMass;
    p;
}

def uranus(){
    var p = Body();
    p.x = 12.8943695621391310;
    p.y = -15.1111514016986312;
    p.z = -0.223307578892655734;
    p.vx = 0.00296460137564761618 * daysPerYear;
    p.vy = 0.00237847173959480950 * daysPerYear;
    p.vz = -0.0000296589568540237556 * daysPerYear;
    p.mass = 0.0000436624404335156298 * solarMass;
    p;
}

def neptune(){
    var p = Body();
    p.x = 15.3796971148509165;
    p.y = -25.9193146099879641;
    p.z = 0.179258772950371181;
    p.vx = 0.00268067772490389322 * daysPerYear;
    p.vy = 0.00162824170038242295 * daysPerYear;
    p.vz = -0.0000951592254519715870 * daysPerYear;
    p.mass = 0.0000515138902046611451 * solarMass;
    p;
}

def sun(){
    var p = Body();
    p.mass = solarMass;
    p;
}


use(parser);

def Node __format__(
    Node this, 
    HashMap«String, Node» data) 
(
    target(Node), 
    doc("Replace every internal identifier (an identifier prefixed with a '%' character) in the specified AST tree with the corresponding AST tree."),
)
{
    res := this;
    data.each(key, value)
    {
	res = res.replace(Identifier(?, key), value);
    }
    return res;
}

def Int callTo?(
    Node this,
    String name)
(
    target(Node),
    doc("Returns true if the specified AST node is a call node and the function node is an identifier with the specified name."),
)
{
    if(((this as Call).function as Identifier).name == name) { 1 } else { ? }
}

def Int named?(
    Node this,
    String name)
(
    target(Node),
    doc("Returns true if the specified AST node is an identifier with the specified name."),
)
{
    if((this as Identifier).name == name) { 1 } else { ? }
}

def Int block?(
    Node this)
(
    target(Node),
    doc("Returns true if the specified AST node is a block."),
)
{
    if(((this as Call).function as Identifier).name == "__block__") { 1 } else { ? }
}

def List«Call» filterByCall(Call this, String name)
(
    target(Call),
)
{
    res := «Call»[];
    this.each(chld)
    {
	if(callTo?(chld, name)){ res.push(chld as Call) }
    }
    return res;
}

def Int childNamed?(Call this, String name)
(
    target(Call),
)
{
    this.each(chld)
    {
	if(named?(chld, name)){ return 1 }
    }
    return ?;
}


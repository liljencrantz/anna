/*
  A bunch of additional functions to be inserted into the basic builtin types
 */

def String hashMapToString(HashMap this)
(
    name(toString), 
    target(HashMap), 
    doc("Returns a String representation of this HashMap."),
)
{
    return "[" ~ ", ".join(this.map(key, value){"" ~ key ~ " : " ~ (value or "null")}) ~ "]";
}

def String intFormat(Int this, Int base=10)
(
    name(format), 
    target(Int), 
    doc("Returns a formated version of this string in the specified base."),
)
{
    digits := "0123456789abcdefghijklmnopqrstuvwxyz";

    if((base > 36) or (base < 2))
    {
	return ?;
    }

    res := "";
    neg := "";
    if(this < 0)
    {
	neg = "-";
	this = -this;
    }
    
    if(this == 0)
    {
	res = "0";
    } 
    else 
    {
	while(this != 0)
	{
	    res = "" ~ digits[this ^mod base] ~ res;
	    this = this / base;
	}
    }
    
    return neg ~ res;
}

def String pairToString(Pair this)
(
    name(toString), 
    target(Pair), 
    doc("Returns a String representation of this Pair."),
)
{
    return "" ~ (this.first or "null") ~ "|" ~ (this.second or "null");
}

def Int pairHashCode(Pair this)
(
    name(hashCode), 
    target(Pair), 
    doc("Returns a hash code for this Pair."),
)
{
    this.first.hashCode() ^bitand this.second.hashCode()
}

def Int pairCmp(Pair this, Object other)
(
    name(__cmp__), 
    target(Pair), 
    doc("Compares two pairs."),
)
{
    (that := other as Pair) or (return ?);
    cmp1 := this.first.__cmp__(that.first);
    if(cmp1 == 0)
    {
	return this.second.__cmp__(that.second);
    } 
    else
    {
	return cmp1;
    }    
}

def HashMap hashMapSetAll(HashMap this, Pair value...)
(
    name(__setAll__), 
    target(HashMap), 
)
{
    value.each(pair)
    {
	this[pair.first] = pair.second;
    }
    return this;
}



macro enum(definition)
{
    var body = definition[2] as Call;
    definition.function = ast(type);
    
    var newBody = ast(
	{
	    var Int value;
	}
	);

    var nameBody = ast(
	{
	    if( !this.value)
	    {
		return "null";
	    }
	}
	);

    var init = ast(def __init__(Int value){this.value = value});

    body.each(idx,el)
    {
	var id = el as Identifier;
	if(!id)
	{
	    definition.error("Unexpected thingie");
	    return null;
	}
	var getter = "!" ~ id.name ~ "Getter";
	var fun = ast(def placeholder1() (static) {return placeholder2;});
	var prop = ast(var Int placeholder1 (getter(placeholder2)));
	var cmp = ast(if(this.value == placeholder1){return placeholder2});

	fun = fun.replace(ast(placeholder1),Identifier(definition, getter))
	    .replace(ast(placeholder2),IntLiteral(definition, idx)) as Call;
	prop = prop.replace(ast(placeholder1),Identifier(definition, id.name))
	    .replace(ast(placeholder2), Identifier(definition, getter)) as Call;
	cmp = cmp.replace(ast(placeholder1),IntLiteral(definition, idx))
	    .replace(ast(placeholder2), StringLiteral(definition, id.name)) as Call;

	newBody ~= [fun];
	newBody ~= [prop];
	nameBody ~= [cmp]
    }

    newBody ~= [init];
    
    var nameFun = ast(def toString(){});
    nameFun[4] = nameBody;
    newBody ~= [nameFun];
    
    definition[2] = newBody;

    print(definition,"\n");	    
    return definition;
}

